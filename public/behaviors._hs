behavior Menu
    on click 
        toggle .hidden on next .menu
        set :opened to not :opened
    on click from document 
        if target is not me and target is not in .menu and :opened 
            add .hidden to next .menu
            set :opened to false
end           

behavior Draggable(dragHandle)
    init
      if no dragHandle set the dragHandle to me
    end
    on pointerdown(clientX, clientY) from dragHandle
      halt the event
      trigger draggable:start -- hooks, e.g. for adding a drop shadow while dragging
      measure my x, y
      set xoff to clientX - x
      set yoff to clientY - y
      repeat until event pointerup from document
        wait for pointermove(pageX, pageY) or
                 pointerup  (pageX, pageY) from document
        add { left: ${pageX - xoff}px; top: ${pageY - yoff}px; }
        trigger draggable:move
      end
      trigger draggable:end
end